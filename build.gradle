import io.github.fvarrui.javapackager.gradle.PackageTask

buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/google' }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.7.5'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}
apply plugin: 'io.github.fvarrui.javapackager.plugin'


group 'indi.yiyi.stockmonitor'
version '1.0-SNAPSHOT'



ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'indi.yiyi.stockmonitor.StockApp'
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'io.github.nonoas:jfx-flat-ui:1.0.2'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

tasks.register('packageMyApp', PackageTask) {
    dependsOn clean, jar
    // mandatory
    mainClass = mainClassName
    // optional
    modules = ["java.base", "java.management", "java.net.http", "java.scripting", "java.sql", "java.naming",
               "jdk.jsobject", "jdk.unsupported", "jdk.unsupported.desktop", "jdk.xml.dom"]
    bundleJre = true
    generateInstaller = false
    administratorRequired = false
    winConfig { createZipball = true }

}
